import static org.gradle.api.file.DuplicatesStrategy.INCLUDE

plugins {
    id 'com.exactpro.th2.artifacts-convensions'
    id "com.google.protobuf" version "0.9.1"
}

ext {
    grpcVersion         = '1.32.1'
    protobufVersion     = '3.13.0'
    genDir              = file("${project.projectDir}/src/gen")
    serviceGeneratorVersion = '3.2.2'
}

dependencies {
    api "com.google.protobuf:protobuf-java-util"
    api "io.grpc:grpc-stub"
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-core"
    implementation "io.grpc:grpc-netty"

    implementation "javax.annotation:javax.annotation-api:1.3.2"

    implementation "com.exactpro.th2:grpc-service-generator:$serviceGeneratorVersion"
}

sourceSets {
    main {
        proto {
            srcDirs 'src/main/proto'
        }
        java {
            srcDirs 'src/gen/main/java', 'src/gen/main/grpc', 'src/gen/main/services/java/src'
        }
        resources {
            srcDirs 'src/gen/main/services/java/resources'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
        services {
            artifact = "com.exactpro.th2:grpc-service-generator:${serviceGeneratorVersion}:all@jar"
        }
    }
    generateProtoTasks.generatedFilesBaseDir = genDir
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            services {
                option 'javaInterfacesPath=./java/src'
                option 'javaInterfacesImplPath=./java/src'
                option 'javaMetaInfPath=./java/resources'
                option 'pythonPath=./python'
            }
        }
        ofSourceSet('main')
    }
}

compileJava.dependsOn.add('generateProto')

tasks.getByName("processResources") {
    dependsOn('generateProto')
    duplicatesStrategy = INCLUDE
}
tasks.getByName("sourcesJar") {
    dependsOn('generateProto')
    duplicatesStrategy = INCLUDE
}

clean {
    delete genDir
}
