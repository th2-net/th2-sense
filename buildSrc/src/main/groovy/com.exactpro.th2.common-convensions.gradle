plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

group 'com.exactpro.th2'
version release_version

sourceCompatibility = 11
targetCompatibility = 11

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

dependencies {
    implementation platform('com.exactpro.th2:bom:3.2.0')
    implementation 'org.slf4j:slf4j-api'
    implementation 'io.github.microutils:kotlin-logging:2.1.23'

    testImplementation('org.apache.logging.log4j:log4j-slf4j-impl') {
        because('logging in testing')
    }
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'io.strikt:strikt-core:0.34.1'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'nexus'
            credentials {
                username = "${nexus_user}"
                password = "${nexus_password}"
            }
            url = "${nexus_url}"
        }
    }
}